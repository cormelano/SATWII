

#include <ppc-asm.h>
#include <ogc/machine/asm.h>

.extern sh2_Read8
.extern sh2_Read16
.extern sh2_Read32
.extern sh2_Write8
.extern sh2_Write16
.extern sh2_Write32

//For each function the registers are r3 and r4 for input
//the special registers
//have the following mapping
// sh_r0	| r14
// pc		| r23
// pr		| r24
// mach		| r25
// macl		| r26
// vbr		| r27
// gbr		| r28
// cycles	| r29
// sr		| r30
// sh_ctx	| r31

//Note on every instruction handler:
//1) The cycles update is the last thing done, this is
// becase here we setup the status bits to check if
// all cycles are done when returning
//2) To fetch the instruction we use sh2_GetPCAddr
// and only use it when branching, else we just increment the
// pointer
//3)


#define sh_pca     20
#define sh_raddr   21
#define sh_instr   22
#define rCycles    23
#define sh_macl    24
#define sh_mach    25
#define sh_vbr     26
#define sh_gbr     27
#define sh_pr      28
#define sh_pc      29
#define sh_sr      30

//Load Rn in r3 and Rm in r4, r6 sets the
.macro __LOAD_REG_R0 r
	lwz \r, 0(r31)
.endm

.macro __LOAD_REG_R15 r
	lwz \r, (15*4)(r31)
.endm

.macro __LOAD_REG_RN r
	rlwinm sh_raddr, sh_instr, 32-6, 26, 29
	lwzx \r, sh_raddr, r31
.endm

.macro __LOAD_REG_RM r
	rlwinm \r, sh_instr, 32-2, 26, 29
	lwzx \r, \r, r31
.endm

.macro __LOAD_REG_RN_RM rn rm
	__LOAD_REG_RM \rm
	__LOAD_REG_RN \rn
.endm

.macro __LOAD_REG_RN_RM_R0 rn rm rz
	__LOAD_REG_R0 \rz
	__LOAD_REG_RM \rm
	__LOAD_REG_RN \rn
.endm

.macro __LOAD_IMM_U8 r
	andi. \r, sh_instr, 0xFF
.endm

.macro __LOAD_IMM_S8 r
	extsb \r, sh_instr
.endm

.macro __LOAD_IMM_S12x2 r
	rlwinm \r, sh_instr, 20, 0, 31
	srawi \r, \r, 19
.endm

.macro __STORE_REG_R0 r
	stw \r, 0(r31)
.endm

.macro __STORE_REG_R15 r
	stw \r, (15*4)(r31)
.endm

.macro __STORE_REG_RN rn
	stwx \rn, sh_raddr, r31
.endm

.macro __STORE_REG_RN_ALL rval, r_addr
	rlwinm \r_addr, sh_instr, 32-6, 26, 29
	stwx \rval, \r_addr, r31
.endm

.macro __STORE_REG_RM_ALL rval, r_addr
	rlwinm \r_addr, sh_instr, 32-2, 26, 29
	stwx \rval, \r_addr, r31
.endm

.macro __STORE_REG_RM_TMP r rtmp
	stwx \r, \rtmp, r31
.endm

//r holds the real address of the delay slot instruction
.macro __SET_DELAY_SLOT_
	lhzu sh_instr, 2(sh_pca)
.endm

.macro __DO_DELAY_SLOT_ // Calls the delayslot instr stored in sh_instr
	//TODO: check if sh_instr is branch -> illegal branch instruction
	addi sh_pc, sh_pc, -2
	b .decode_insr
.endm

//Gets the address of the pc in host ram
.macro __GET_PC_ADDR
	mr r3, sh_pc
	bl sh2_GetPCAddr
	addi sh_pca, r3, -2
.endm

.text


FUNC_START(sh2_int_ADD) /* ADD Rm,Rn  0011nnnnmmmm1100 */
	__LOAD_REG_RN_RM r3, r4
	add r3, r3, r4
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_ADD)

FUNC_START(sh2_int_ADDI) /* ADD #imm,Rn  0111nnnniiiiiiii */
	__LOAD_REG_RN r3
	__LOAD_IMM_S8 r4
	add r3, r3, r4
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_ADDI)

FUNC_START(sh2_int_ADDC) /* ADDC Rm,Rn  0011nnnnmmmm1110 */
	__LOAD_REG_RN_RM r3, r4
	rlwinm r5, sh_sr, 29, 2, 2
	mtxer r5
	addeo r3, r3, r4
	mfxer r5
	rlwimi sh_sr, r5, 3, 31, 31
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_ADDC)

FUNC_START(sh2_int_ADDV) /* ADDV Rm,Rn  0011nnnnmmmm1111 */
	__LOAD_REG_RN_RM r3, r4
	addo r3, r3, r4
	mfxer r5
	rlwimi sh_sr, r5, 2, 31, 31
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_ADDV)

FUNC_START(sh2_int_SUB) /* SUBC Rm,Rn  0011nnnnmmmm1010*/
	__LOAD_REG_RN_RM r3, r4
	subf r3, r4, r3
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_SUB)

FUNC_START(sh2_int_SUBC) /* SUBC Rm,Rn  0011nnnnmmmm1010*/
	__LOAD_REG_RN_RM r3, r4
	xori sh_sr, sh_sr, 1
	rlwinm r5, sh_sr, 29, 2, 2
	mtxer r5
	subfeo r3, r4, r3
	mfxer r5
	rlwimi sh_sr, r5, 3, 31, 31
	xori sh_sr, sh_sr, 1
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_SUBC)

FUNC_START(sh2_int_SUBV) /* SUBV Rm,Rn  0011nnnnmmmm1011*/
	__LOAD_REG_RN_RM r3, r4
	subfo r3, r4, r3
	mfxer r5
	rlwimi sh_sr, r5, 2, 31, 31
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_SUBV)

FUNC_START(sh2_int_AND) /* AND Rm,Rn  0010nnnnmmmm1001*/
	__LOAD_REG_RN_RM r3, r4
	and  r3, r3, r4
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_AND)

FUNC_START(sh2_int_ANDI) /* AND #imm,R0  11001001iiiiiiii*/
	__LOAD_REG_R0 r3
	__LOAD_IMM_U8 r4
	and  r3, r3, r4
	__STORE_REG_R0 r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_ANDI)

FUNC_START(sh2_int_ANDM) /* AND.B #imm,@(R0,GBR)  11001101iiiiiiii*/
	__LOAD_REG_R0 r3
	add r3, r3, sh_gbr
	bl sh2_Read8
	__LOAD_IMM_U8 r4
	and r4, r3, r4
	__LOAD_REG_R0 r3
	add r3, r3, sh_gbr
	bl sh2_Write8
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 3
	b cycle_check
FUNC_END(sh2_int_ANDM)

FUNC_START(sh2_int_OR) /* OR Rm,Rn  0010nnnnmmmm1011 */
	__LOAD_REG_RN_RM r3, r4
	or  r3, r3, r4
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_OR)

FUNC_START(sh2_int_ORI) /* OR #imm,R0  11001011iiiiiiii */
	__LOAD_REG_R0 r3
	__LOAD_IMM_U8 r4
	or  r3, r3, r4
	__STORE_REG_R0 r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_ORI)

FUNC_START(sh2_int_ORM) /* OR.B #imm,@(R0,GBR)  11001111iiiiiiii */
	__LOAD_REG_R0 r3
	add r3, r3, sh_gbr
	bl sh2_Read8
	__LOAD_IMM_U8 r4
	or r4, r3, r4
	__LOAD_REG_R0 r3
	add r3, r3, sh_gbr
	bl sh2_Write8
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 3
	b cycle_check
FUNC_END(sh2_int_ORM)

FUNC_START(sh2_int_XOR) /* XOR Rm,Rn  0010nnnnmmmm1010 */
	__LOAD_REG_RN_RM r3, r4
	xor  r3, r3, r4
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_XOR)

FUNC_START(sh2_int_XORI) /* XOR #imm,R0  11001010iiiiiiii */
	__LOAD_REG_R0 r3
	__LOAD_IMM_U8 r4
	xor  r3, r3, r4
	__STORE_REG_R0 r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_XORI)

FUNC_START(sh2_int_XORM) /* XOR.B #imm,@(R0,GBR)  11001110iiiiiiii */
	__LOAD_REG_R0 r3
	add r3, r3, sh_gbr
	bl sh2_Read8
	__LOAD_IMM_U8 r4
	xor r4, r3, r4
	__LOAD_REG_R0 r3
	add r3, r3, sh_gbr
	bl sh2_Write8
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 3
	b cycle_check
FUNC_END(sh2_int_XORM)

FUNC_START(sh2_int_ROTCL) /* ROTCL Rn  0100nnnn00100100 */
	__LOAD_REG_RN r3
	rlwinm r3, r3, 1, 0, 31
	andi. r5, r3, 1
	rlwimi r3, sh_sr, 0, 31, 31
	rlwimi sh_sr, r5, 0, 31, 31
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_ROTCL)

FUNC_START(sh2_int_ROTCR) /* ROTCR Rn  0100nnnn00100101 */
	__LOAD_REG_RN r3
	andi. r5, r3, 1
	rlwimi r3, sh_sr, 0, 31, 31
	rlwinm r3, r3, 32-1, 0, 31
	rlwimi sh_sr, r5, 0, 31, 31
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_ROTCR)

FUNC_START(sh2_int_ROTL) /* ROTL Rn  0100nnnn00000100 */
	__LOAD_REG_RN r3
	rlwimi sh_sr, r3, 1, 31, 31
	rlwinm r3, r3, 1, 0, 31
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_ROTL)

FUNC_START(sh2_int_ROTR)
	__LOAD_REG_RN r3
	rlwimi sh_sr, r3, 0, 31, 31
	rlwinm r3, r3, 32-1, 0, 31
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_ROTR)

FUNC_START(sh2_int_SHAL)
	__LOAD_REG_RN r3
	rlwimi sh_sr, r3, 1, 31, 31
	rlwinm r3, r3, 1, 0, 30
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_SHAL)

FUNC_START(sh2_int_SHAR)
	__LOAD_REG_RN r3
	rlwimi sh_sr, r3, 0, 31, 31
	srawi r3, r3, 1
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_SHAR)

FUNC_START(sh2_int_SHLL)
	// Does the same thing as SHAL
	__LOAD_REG_RN r3
	rlwimi sh_sr, r3, 1, 31, 31
	rlwinm r3, r3, 1, 0, 30
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_SHLL)

FUNC_START(sh2_int_SHLL2)
	__LOAD_REG_RN r3
	rlwinm r3, r3, 2, 0, 31-2
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_SHLL2)

FUNC_START(sh2_int_SHLL8)
	__LOAD_REG_RN r3
	rlwinm r3, r3, 8, 0, 31-8
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_SHLL8)

FUNC_START(sh2_int_SHLL16)
	__LOAD_REG_RN r3
	rlwinm r3, r3, 16, 0, 31-16
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_SHLL16)

FUNC_START(sh2_int_SHLR)
	__LOAD_REG_RN r3
	rlwimi sh_sr, r3, 0, 31, 31
	rlwinm r3, r3, 32-1, 1, 31
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_SHLR)

FUNC_START(sh2_int_SHLR2)
	__LOAD_REG_RN r3
	rlwinm r3, r3, 32-2, 2, 31
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_SHLR2)

FUNC_START(sh2_int_SHLR8)
	__LOAD_REG_RN r3
	rlwinm r3, r3, 32-8, 8, 31
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_SHLR8)

FUNC_START(sh2_int_SHLR16)
	__LOAD_REG_RN r3
	rlwinm r3, r3, 32-16, 16, 31
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_SHLR16)

FUNC_START(sh2_int_NOT) /* NOT Rm,Rn  0110nnnnmmmm0111 */
	__LOAD_REG_RM r4
	nor r4, r4, r4
	__STORE_REG_RN_ALL r4, r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_NOT)

FUNC_START(sh2_int_NEG) /* NEG Rm,Rn  0110nnnnmmmm1011 */
	__LOAD_REG_RM r4
	neg r4, r4
	__STORE_REG_RN_ALL r4, r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_NEG)

FUNC_START(sh2_int_NEGC) /* NEGC Rm,Rn  0110nnnnmmmm1010 */
	__LOAD_REG_RM r4
	xori r3, sh_sr, 1
	rlwinm r3, r3, 29, 2, 2
	mtxer r3
	subfze r4, r4
	mfxer r3
	rlwimi sh_sr, r3, 3, 31, 31
	xori sh_sr, sh_sr, 1
	__STORE_REG_RN_ALL r4, r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_NEGC)

FUNC_START(sh2_int_DT) /* DT Rn  0100nnnn00010000 */
	__LOAD_REG_RN r3
	addic. r3, r3, -1
	__STORE_REG_RN r3
	mfcr r3
	rlwimi sh_sr, r3, 3, 31, 31
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_DT)

FUNC_START(sh2_int_EXTSB) /* EXTS.B Rm,Rn  0110nnnnmmmm1110 */
	__LOAD_REG_RM r4
	extsb r4, r4
	__STORE_REG_RN_ALL r4, r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_EXTSB)

FUNC_START(sh2_int_EXTSW) /* EXTS.W Rm,Rn  0110nnnnmmmm1111 */
	__LOAD_REG_RM r4
	extsh r4, r4
	__STORE_REG_RN_ALL r4, r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_EXTSW)

FUNC_START(sh2_int_EXTUB) /* EXTU.B Rm,Rn  0110nnnnmmmm1100 */
	__LOAD_REG_RM r4
	andi. r4, r4, 0x00FF
	__STORE_REG_RN_ALL r4, r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_EXTUB)

FUNC_START(sh2_int_EXTUW) /* EXTU.W Rm,Rn  0110nnnnmmmm1101 */
	__LOAD_REG_RM r4
	andi. r4, r4, 0xFFFF
	__STORE_REG_RN_ALL r4, r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_EXTUW)


/*Mult and Division*/

FUNC_START(sh2_int_DIV0S)
	__LOAD_REG_RN_RM r3, r4
	rlwimi sh_sr, r3, 8+1, 31-8, 31-8
	rlwimi sh_sr, r4, 9+1, 31-9, 31-9
	xor r3, r3, r4
	rlwimi sh_sr, r3, 1, 31, 31
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_DIV0S)


FUNC_START(sh2_int_DIV0U)
	andi. sh_sr, sh_sr, 0xFCFE
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_DIV0U)

FUNC_START(sh2_int_DIV1)
	__LOAD_REG_RN_RM r3, r4
	mtcrf	0x07, sh_sr
	creqv	3, 31-9, 31-8	//(M == oldQ)
	bc	    0b00100, 3,	div1_skip_rm_neg // do not negate if false
	neg     r4, r4					//Rm = -Rm
div1_skip_rm_neg:
	mtcrf	0x80, r3			//Q = MSB(Rn)
	crxor   31-8, 0, 31-8		//Q ^= oldQ
	rlwimi  sh_sr, r3, 1, 0, 30	// Rn = (Rn << 1) | T
	addc    r3, sh_sr, r4			// Rn += Rm
	mcrxr   0					// Q ^= carry(Rn += Rm)
	creqv   31-8, 2, 31-8
	creqv   31, 31-8, 31-9		//T = (Q == M)
	__STORE_REG_RN r3
	mfcr    r5
	andi.	sh_sr, r5, 0xFFF
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_DIV1)

FUNC_START(sh2_int_DMULS)
	__LOAD_REG_RN_RM r3, r4
	mulhw sh_mach, r3, r4
	mullw sh_macl, r3, r4
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 2
	b cycle_check
FUNC_END(sh2_int_DMULS)

FUNC_START(sh2_int_DMULU)
	__LOAD_REG_RN_RM r3, r4
	mulhwu sh_mach, r3, r4
	mullw sh_macl, r3, r4
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_DMULU)

FUNC_START(sh2_int_MACL)
	__LOAD_REG_RM r3
	addi r4, r3, 4
	__STORE_REG_RM_ALL r4, r5
	bl sh2_Read32
	mr sh_raddr, r3
	__LOAD_REG_RN r3
	addi r4, r3, 4
	__STORE_REG_RN r4
	bl sh2_Read32
	mr r4, sh_raddr
	mullw r9,r3,r4
	xor r8,r3,r4
	rlwinm r5, r30, 31,31,31
	addi r10,r5,-1
	mulhw r3,r3,r4
	addc r9,r9,sh_macl
	and r9,r9,r10
	adde r3,r3,sh_mach
	srawi r8,r8,31
	subfic r5,r5,0
	xori sh_macl,r8,0x7fff
	subfe sh_mach,sh_mach,sh_mach
	and r3,r3,r10
	andc r5,r5,r8
	and sh_macl,sh_macl,sh_mach
	or sh_macl,r9,r5        	 //store macl
	or sh_mach,r3,sh_macl        //store mach
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 2
	b cycle_check
FUNC_END(sh2_int_MACL)

FUNC_START(sh2_int_MACW)
//XXX
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 2
	b cycle_check
FUNC_END(sh2_int_MACW)

FUNC_START(sh2_int_MULL)
	__LOAD_REG_RN_RM r3, r4
	mullw sh_macl, r3, r4
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 2
	b cycle_check
FUNC_END(sh2_int_MULL)

FUNC_START(sh2_int_MULS)
	__LOAD_REG_RN_RM r3, r4 //NOTE: can load aritmetic half word and avoid the two extsh
	extsh r3, r3
	extsh r4, r4
	mullw sh_macl, r3, r4
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MULS)

FUNC_START(sh2_int_MULU)
	__LOAD_REG_RN_RM r3, r4 //NOTE: can load unsigned half word and avoid the two and
	andi. r3, r3, 0xFFFF
	andi. r4, r4, 0xFFFF
	mullw sh_macl, r3, r4
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MULU)


/*Set and Clear*/
FUNC_START(sh2_int_CLRMAC) /* CLRMAC  0000000000101000 */
	li sh_mach, 0
	li sh_macl, 0
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_CLRMAC)

FUNC_START(sh2_int_CLRT) /* CLRT  0000000000001000 */
	andi. sh_sr, sh_sr, 0xFFFE
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_CLRT)

FUNC_START(sh2_int_SETT) /* SETT  0000000000011000 */
	ori sh_sr, sh_sr, 0x1
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_SETT)

/*Compare*/
FUNC_START(sh2_int_CMPEQ) /* CMP_EQ Rm,Rn  0011nnnnmmmm0000 */
	__LOAD_REG_RN_RM r3, r4
	cmpw cr0, r3, r4
	mfcr r3
	rlwimi sh_sr, r3, 3, 31, 31
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_CMPEQ)

FUNC_START(sh2_int_CMPGE) /* CMP_GE Rm,Rn  0011nnnnmmmm0011 */
	__LOAD_REG_RN_RM r3, r4
	cmpw cr0, r3, r4
	crnor 0, 0, 0
	mfcr r3
	rlwimi sh_sr, r3, 1, 31, 31
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_CMPGE)

FUNC_START(sh2_int_CMPGT) /* CMP_GT Rm,Rn  0011nnnnmmmm0111 */
	__LOAD_REG_RN_RM r3, r4
	cmpw cr0, r3, r4
	mfcr r3
	rlwimi sh_sr, r3, 2, 31, 31
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_CMPGT)

FUNC_START(sh2_int_CMPHI) /* CMP_HI Rm,Rn  0011nnnnmmmm0110 */
	__LOAD_REG_RN_RM r3, r4
	cmplw cr0, r3, r4
	mfcr r3
	rlwimi sh_sr, r3, 2, 31, 31
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_CMPHI)

FUNC_START(sh2_int_CMPHS) /* CMP_HS Rm,Rn  0011nnnnmmmm0010 */
	__LOAD_REG_RN_RM r3, r4
	cmplw cr0, r3, r4
	crnor 0, 0, 0
	mfcr r3
	rlwimi sh_sr, r3, 1, 31, 31
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_CMPHS)

FUNC_START(sh2_int_CMPPL) /* CMP_PL Rn  0100nnnn00010101 */
	__LOAD_REG_RN r3
	neg r3, r3
	rlwimi sh_sr, r3, 1, 31, 31
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_CMPPL)

FUNC_START(sh2_int_CMPPZ) /* CMP_PZ Rn  0100nnnn00010001 */
	__LOAD_REG_RN r3
	rlwimi sh_sr, r3, 1, 31, 31
	xori sh_sr, sh_sr, 1
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_CMPPZ)

FUNC_START(sh2_int_CMPSTR) /* CMP_STR Rm,Rn  0010nnnnmmmm1100 */
	__LOAD_REG_RN_RM r3, r4
	xor r5, r3, r4
	addis r3, r0, 0x0101
	ori r3, r3, 0x0101
	subf r4, r3, r5
	andc r5, r4, r5
	rlwinm r5, r5, 1, 0, 31
	and. r5, r5, r3
	mfcr r5
	rlwimi sh_sr, r5, 2, 31, 31
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_CMPSTR)

FUNC_START(sh2_int_CMPIM) /* CMP_EQ #imm,R0  10001000iiiiiiii */
	__LOAD_REG_R0 r3
	__LOAD_IMM_S8 r4
	cmpi cr0, r3, r4
	mfcr r3
	rlwimi sh_sr, r3, 3, 31, 31
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_CMPIM)


/*Load and Stores*/
FUNC_START(sh2_int_LDCSR) /* LDC Rm,SR  0100mmmm00001110 */
	__LOAD_REG_RN r3
	andi. sh_sr, r3, 0x03F3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_LDCSR)

FUNC_START(sh2_int_LDCGBR) /* LDC Rm,GBR  0100mmmm00011110 */
	__LOAD_REG_RN sh_gbr
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_LDCGBR)

FUNC_START(sh2_int_LDCVBR) /* LDC Rm,VBR  0100mmmm00101110 */
	__LOAD_REG_RN sh_vbr
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_LDCVBR)

FUNC_START(sh2_int_LDCMSR) /* LDC.L @Rm+,SR  0100mmmm00000111 */
	__LOAD_REG_RN r3
	addi r4, r3, 4
	__STORE_REG_RN r4
	bl sh2_Read32
	andi. sh_sr, r3, 0x03F3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_LDCMSR)

FUNC_START(sh2_int_LDCMGBR) /* LDC.L @Rm+,GBR  0100mmmm00010111 */
	__LOAD_REG_RN r3
	addi r4, r3, 4
	__STORE_REG_RN r4
	bl sh2_Read32
	mr sh_gbr, r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_LDCMGBR)

FUNC_START(sh2_int_LDCMVBR) /* LDC.L @Rm+,VBR  0100mmmm00100111 */
	__LOAD_REG_RN r3
	addi r4, r3, 4
	__STORE_REG_RN r4
	bl sh2_Read32
	mr sh_vbr, r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_LDCMVBR)

FUNC_START(sh2_int_LDSMACH) /* LDS Rm,MACH  0100mmmm00001010 */
	__LOAD_REG_RN sh_mach
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_LDSMACH)

FUNC_START(sh2_int_LDSMACL) /* LDS Rm,MACL  0100mmmm00011010 */
	__LOAD_REG_RN sh_macl
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_LDSMACL)

FUNC_START(sh2_int_LDSPR) /* LDS Rm,PR  0100mmmm00101010 */
	__LOAD_REG_RN sh_pr
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_LDSPR)

FUNC_START(sh2_int_LDSMMACH) /* LDS.L @Rm+,MACH  0100mmmm00000110 */
	__LOAD_REG_RN r3
	addi r4, r3, 4
	__STORE_REG_RN r4
	bl sh2_Read32
	mr sh_mach, r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_LDSMMACH)

FUNC_START(sh2_int_LDSMMACL) /* LDS.L @Rm+,MACL  0100mmmm00010110 */
	__LOAD_REG_RN r3
	addi r4, r3, 4
	__STORE_REG_RN r4
	bl sh2_Read32
	mr sh_macl, r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_LDSMMACL)

FUNC_START(sh2_int_LDSMPR) /* LDS.L @Rm+,PR  0100mmmm00100110 */
	__LOAD_REG_RN r3
	addi r4, r3, 4
	__STORE_REG_RN r4
	bl sh2_Read32
	mr sh_pr, r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_LDSMPR)

FUNC_START(sh2_int_STCSR) /* STC SR,Rn  0000nnnn00000010 */
	__STORE_REG_RN_ALL sh_sr, r4
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_STCSR)

FUNC_START(sh2_int_STCGBR) /* STC GBR,Rn  0000nnnn00010010 */
	__STORE_REG_RN_ALL sh_gbr, r4
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_STCGBR)

FUNC_START(sh2_int_STCVBR) /* STC VBR,Rn  0000nnnn00100010 */
	__STORE_REG_RN_ALL sh_vbr, r4
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_STCVBR)

FUNC_START(sh2_int_STCMSR) /* STC.L SR,@-Rn  0100nnnn00000011 */
	__LOAD_REG_RN r3
	addi r3, r3, -4
	__STORE_REG_RN r3
	mr r4, sh_sr
	bl sh2_Write32
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_STCMSR)

FUNC_START(sh2_int_STCMGBR) /* STC.L GBR,@-Rn  0100nnnn00010011 */
	__LOAD_REG_RN r3
	addi r3, r3, -4
	__STORE_REG_RN r3
	mr r4, sh_gbr
	bl sh2_Write32
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_STCMGBR)

FUNC_START(sh2_int_STCMVBR) /* STC.L VBR,@-Rn  0100nnnn00100011 */
	__LOAD_REG_RN r3
	addi r3, r3, -4
	__STORE_REG_RN r3
	mr r4, sh_vbr
	bl sh2_Write32
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_STCMVBR)

FUNC_START(sh2_int_STSMACH) /* STS MACH,Rn  0000nnnn00001010 */
	__STORE_REG_RN_ALL sh_mach, r4
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_STSMACH)

FUNC_START(sh2_int_STSMACL) /* STS MACL,Rn  0000nnnn00011010 */
	__STORE_REG_RN_ALL sh_macl, r4
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_STSMACL)

FUNC_START(sh2_int_STSPR) /* STS PR,Rn  0000nnnn00101010 */
	__STORE_REG_RN_ALL sh_pr, r4
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_STSPR)

FUNC_START(sh2_int_STSMMACH) /* STS.L MACH,@–Rn  0100nnnn00000010 */
	__LOAD_REG_RN r3
	addi r3, r3, -4
	__STORE_REG_RN r3
	mr r4, sh_mach
	bl sh2_Write32
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_STSMMACH)

FUNC_START(sh2_int_STSMMACL) /* STS.L MACL,@–Rn  0100nnnn00010010 */
	__LOAD_REG_RN r3
	addi r3, r3, -4
	__STORE_REG_RN r3
	mr r4, sh_macl
	bl sh2_Write32
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_STSMMACL)

FUNC_START(sh2_int_STSMPR) /* STS.L PR,@–Rn  0100nnnn00100010 */
	__LOAD_REG_RN r3
	addi r3, r3, -4
	__STORE_REG_RN r3
	mr r4, sh_pr
	bl sh2_Write32
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_STSMPR)


/*Move Data*/
FUNC_START(sh2_int_MOV) /* MOV Rm,Rn  0110nnnnmmmm0011 */
	__LOAD_REG_RM r3
	__STORE_REG_RN_ALL r3, r4
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOV)

FUNC_START(sh2_int_MOVBS) /* MOV.B Rm,@Rn  0010nnnnmmmm0000 */
	__LOAD_REG_RN_RM r3, r4
	andi. r4, r4, 0xFF
	bl sh2_Write8
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVBS)

FUNC_START(sh2_int_MOVWS) /* MOV.W Rm,@Rn  0010nnnnmmmm0001 */
	__LOAD_REG_RN_RM r3, r4
	andi. r4, r4, 0xFFFF
	bl sh2_Write16
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVWS)

FUNC_START(sh2_int_MOVLS) /* MOV.L Rm,@Rn  0010nnnnmmmm0010 */
	__LOAD_REG_RN_RM r3, r4
	bl sh2_Write32
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVLS)

FUNC_START(sh2_int_MOVBL) /* MOV.B @Rm,Rn  0110nnnnmmmm0000 */
	__LOAD_REG_RM r3
	bl sh2_Read8
	extsb r3, r3
	__STORE_REG_RN_ALL r3, r4
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVBL)

FUNC_START(sh2_int_MOVWL) /* MOV.W @Rm,Rn  0110nnnnmmmm0001 */
	__LOAD_REG_RM r3
	bl sh2_Read16
	extsh r3, r3
	__STORE_REG_RN_ALL r3, r4
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVWL)

FUNC_START(sh2_int_MOVLL) /* MOV.L @Rm,Rn  0110nnnnmmmm0010 */
	__LOAD_REG_RM r3
	bl sh2_Read32
	__STORE_REG_RN_ALL r3, r4
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVLL)

FUNC_START(sh2_int_MOVBM) /* MOV.B Rm,@–Rn  0010nnnnmmmm0100 */
	__LOAD_REG_RN_RM r3, r4
	addi r3, r3, -1
	__STORE_REG_RN r3
	andi. r4, r4, 0xFF
	bl sh2_Write8
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVBM)

FUNC_START(sh2_int_MOVWM) /* MOV.W Rm,@–Rn  0010nnnnmmmm0101 */
	__LOAD_REG_RN_RM r3, r4
	addi r3, r3, -2
	__STORE_REG_RN r3
	andi. r4, r4, 0xFFFF
	bl sh2_Write16
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVWM)

FUNC_START(sh2_int_MOVLM) /* MOV.L Rm,@–Rn  0010nnnnmmmm0110 */
	__LOAD_REG_RN_RM r3, r4
	addi r3, r3, -4
	__STORE_REG_RN r3
	bl sh2_Write32
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVLM)

FUNC_START(sh2_int_MOVBP) /* MOV.B @Rm+,Rn  0110nnnnmmmm0100 */
	rlwinm sh_raddr, sh_instr, 32-6, 26, 29
	rlwinm r4, sh_instr, 32-2, 26, 29
	lwzx r3, r4, r31
	xor. r5, sh_raddr, r4
	mfcr r5
	rlwinm r5, r5, 2, 31, 31 //Make a 1 to add to Rm
	add r5, r3, r5
	stwx r5, r4, r31
	bl sh2_Read8
	extsb r3, r3
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVBP)


FUNC_START(sh2_int_MOVWP) /* MOV.W @Rm+,Rn  0110nnnnmmmm0101 */
	rlwinm sh_raddr, sh_instr, 32-6, 26, 29
	rlwinm r4, sh_instr, 32-2, 26, 29
	lwzx r3, r4, r31
	xor. r5, sh_raddr, r4
	mfcr r5
	rlwinm r5, r5, 3, 30, 30 //Make a 2 to add to Rm
	add r5, r3, r5
	stwx r5, r4, r31
	bl sh2_Read16
	extsh r3, r3
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVWP)


FUNC_START(sh2_int_MOVLP) /* MOV.L @Rm+,Rn  0110nnnnmmmm0110 */
	rlwinm sh_raddr, sh_instr, 32-6, 26, 29
	rlwinm r4, sh_instr, 32-2, 26, 29
	lwzx r3, r4, r31
	xor. r5, sh_raddr, r4
	mfcr r5
	rlwinm r5, r5, 4, 29, 29 //Make a 4 to add to Rm
	add r5, r3, r5
	stwx r5, r4, r31
	bl sh2_Read32
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVLP)

FUNC_START(sh2_int_MOVBS0) /* MOV.B Rm,@(R0,Rn)  0000nnnnmmmm0100 */
	__LOAD_REG_RN_RM_R0 r3, r4, r5
	add r3, r3, r5
	andi. r4, r4, 0xFF
	bl sh2_Write8
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVBS0)

FUNC_START(sh2_int_MOVWS0) /* MOV.W Rm,@(R0,Rn)  0000nnnnmmmm0101 */
	__LOAD_REG_RN_RM_R0 r3, r4, r5
	add r3, r3, r5
	andi. r4, r4, 0xFFFF
	bl sh2_Write16
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVWS0)

FUNC_START(sh2_int_MOVLS0) /* MOV.L Rm,@(R0,Rn)  0000nnnnmmmm0110 */
	__LOAD_REG_RN_RM_R0 r3, r4, r5
	add r3, r3, r5
	bl sh2_Write32
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVLS0)

FUNC_START(sh2_int_MOVBL0) /* MOV.B @(R0,Rm),Rn  0000nnnnmmmm1100 */
	__LOAD_REG_R0 r3
	__LOAD_REG_RM r4
	add r3, r3, r4
	bl sh2_Read8
	extsb r3, r3
	__STORE_REG_RN_ALL r3, r4
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVBL0)

FUNC_START(sh2_int_MOVWL0) /* MOV.W @(R0,Rm),Rn  0000nnnnmmmm1101 */
	__LOAD_REG_R0 r3
	__LOAD_REG_RM r4
	add r3, r3, r4
	bl sh2_Read16
	extsh r3, r3
	__STORE_REG_RN_ALL r3, r4
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVWL0)

FUNC_START(sh2_int_MOVLL0) /* MOV.L @(R0,Rm),Rn  0000nnnnmmmm1110 */
	__LOAD_REG_R0 r3
	__LOAD_REG_RM r4
	add r3, r3, r4
	bl sh2_Read32
	__STORE_REG_RN_ALL r3, r4
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVLL0)

FUNC_START(sh2_int_MOVI) /* MOV #imm,Rn  1110nnnniiiiiiii */
	__LOAD_IMM_S8 r3
	__STORE_REG_RN_ALL r3, r4
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVI)

FUNC_START(sh2_int_MOVWI) /* MOV.W @(disp,PC),Rn  1001nnnndddddddd */
	rlwinm r4, sh_instr, 1, 23, 30	//Load imm << 1
	addi r4, r4, 4
	lhax r3, r4, sh_pca
	__STORE_REG_RN_ALL r3, r4
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVWI)

FUNC_START(sh2_int_MOVLI) /* MOV.L @(disp,PC),Rn  1101nnnndddddddd */
	rlwinm r4, sh_instr, 2, 22, 29	//Load imm << 2
	addi r4, r4, 4
	rlwinm r5, sh_pca, 0, 0, 29 	// clear first two LSB
	lwzx r3, r4, r5
	__STORE_REG_RN_ALL r3, r4
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVLI)

FUNC_START(sh2_int_MOVBLG) /* MOV.B @(disp,GBR),R0  11000100dddddddd */
	rlwinm r3, sh_instr, 0, 24, 31	//Load disp
	add r3, r3, sh_gbr
	bl sh2_Read8
	extsb r3, r3
	__STORE_REG_R0 r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVBLG)

FUNC_START(sh2_int_MOVWLG) /* MOV.W @(disp,GBR),R0  11000101dddddddd */
	rlwinm r3, sh_instr, 1, 23, 30	//Load disp << 1
	add r3, r3, sh_gbr
	bl sh2_Read16
	extsh r3, r3
	__STORE_REG_R0 r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVWLG)

FUNC_START(sh2_int_MOVLLG) /* MOV.L @(disp,GBR),R0  11000110dddddddd */
	rlwinm r3, sh_instr, 2, 22, 29	//Load disp << 2
	add r3, r3, sh_gbr
	bl sh2_Read32
	__STORE_REG_R0 r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVLLG)

FUNC_START(sh2_int_MOVBSG) /* MOV.B R0,@(disp,GBR)  11000000dddddddd */
	rlwinm r3, sh_instr, 0, 24, 31	//Load disp
	add r3, r3, sh_gbr
	__LOAD_REG_R0 r4
	andi. r4, r4, 0xFF
	bl sh2_Write8
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVBSG)

FUNC_START(sh2_int_MOVWSG) /* MOV.W R0,@(disp,GBR)  11000001dddddddd */
	rlwinm r3, sh_instr, 1, 23, 30	//Load disp << 1
	add r3, r3, sh_gbr
	__LOAD_REG_R0 r4
	andi. r4, r4, 0xFFFF
	bl sh2_Write16
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVWSG)

FUNC_START(sh2_int_MOVLSG) /* MOV.L R0,@(disp,GBR)  11000010dddddddd */
	rlwinm r3, sh_instr, 2, 22, 29	//Load disp << 2
	add r3, r3, sh_gbr
	__LOAD_REG_R0 r4
	bl sh2_Write32
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVLSG)

FUNC_START(sh2_int_MOVBS4) /* MOV.B R0,@(disp,Rn)  10000000nnnndddd */
	__LOAD_REG_RM r3
	rlwinm r4, sh_instr, 0, 28, 31	//Load disp
	add r3, r3, r4
	__LOAD_REG_R0 r4
	andi. r4, r4, 0xFF
	bl sh2_Write8
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVBS4)

FUNC_START(sh2_int_MOVWS4) /* MOV.W R0,@(disp,Rn)  10000001nnnndddd */
	__LOAD_REG_RM r3
	rlwinm r4, sh_instr, 1, 27, 30	//Load disp << 1
	add r3, r3, r4
	__LOAD_REG_R0 r4
	andi. r4, r4, 0xFFFF
	bl sh2_Write16
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVWS4)

FUNC_START(sh2_int_MOVLS4) /* MOV.L Rm,@(disp,Rn)  0001nnnnmmmmdddd */
	__LOAD_REG_RN_RM r3, r4
	rlwinm r5, sh_instr, 2, 26, 29	//Load disp << 2
	add r3, r3, r5
	bl sh2_Write32
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVLS4)

FUNC_START(sh2_int_MOVBL4) /* MOV.B @(disp,Rm),R0  10000100mmmmdddd */
	__LOAD_REG_RM r3
	rlwinm r4, sh_instr, 0, 28, 31	//Load disp
	add r3, r3, r4
	bl sh2_Read8
	extsb r3, r3
	__STORE_REG_R0 r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVBL4)

FUNC_START(sh2_int_MOVWL4) /* MOV.W @(disp,Rm),R0  10000101mmmmdddd */
	__LOAD_REG_RM r3
	rlwinm r4, sh_instr, 1, 27, 30	//Load disp << 1
	add r3, r3, r4
	bl sh2_Read16
	extsh r3, r3
	__STORE_REG_R0 r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVWL4)

FUNC_START(sh2_int_MOVLL4) /* MOV.L @(disp,Rm),Rn  0101nnnnmmmmdddd */
	__LOAD_REG_RM r3
	rlwinm r4, sh_instr, 2, 26, 29	//Load disp << 2
	add r3, r3, r4
	bl sh2_Read32
	__STORE_REG_RN_ALL r3, r4
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVLL4)

FUNC_START(sh2_int_MOVA) /* MOVA @(disp,PC),R0  11000111dddddddd */
	rlwinm r3, sh_instr, 2, 22, 29	//Load imm << 2
	addi r3, r3, 4
	rlwinm r4, sh_pc, 0, 0, 29 	// clear first two LSB
	add r3, r3, r4
	__STORE_REG_R0 r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVA)

FUNC_START(sh2_int_MOVT) /* MOVT Rn  0000nnnn00101001 */
	andi. r3, sh_sr, 0x0001
	__STORE_REG_RN_ALL r3, r4
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_MOVT)


/*Branch and Jumps*/
FUNC_START(sh2_int_BF) /* BF disp  10001011dddddddd */
	andi. r3, sh_sr, 1
	bc 0b00100, 2, bf_no_branch //if T == 0 (we skip branch if [EQ] == 0)
	__LOAD_IMM_S8 r3
	rlwinm r3, r3, 1, 0, 30
	addi r3, r3, 4
	add sh_pc, sh_pc, r3
	__GET_PC_ADDR
	addic. rCycles, rCycles, 3
	b cycle_check
bf_no_branch:
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_BF)


FUNC_START(sh2_int_BFS) /* BFS disp  10001111dddddddd */
	andi. r3, sh_sr, 1
	bc 0b00100, 2, bfs_no_branch //if T == 0 (we skip branch if [EQ] == 0)
	__LOAD_IMM_S8 r3
	__SET_DELAY_SLOT_
	rlwinm r3, r3, 1, 0, 30
	addi r3, r3, 4
	add sh_pc, sh_pc, r3
	__GET_PC_ADDR
	addic. rCycles, rCycles, 2
	__DO_DELAY_SLOT_
bfs_no_branch:
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_BFS)

FUNC_START(sh2_int_BRA) /* BRA disp  1010dddddddddddd */
	__LOAD_IMM_S12x2 r4
	__SET_DELAY_SLOT_
	addi sh_pc, sh_pc, 4
	add sh_pc, sh_pc, r4
	__GET_PC_ADDR
	addic. rCycles, rCycles, 2
	__DO_DELAY_SLOT_
FUNC_END(sh2_int_BRA)

FUNC_START(sh2_int_BRAF) /* BRAF Rm  0000mmmm00100011 */
	__LOAD_REG_RN r4
	__SET_DELAY_SLOT_
	addi sh_pc, sh_pc, 4
	add sh_pc, sh_pc, r4
	__GET_PC_ADDR
	addic. rCycles, rCycles, 2
	__DO_DELAY_SLOT_
FUNC_END(sh2_int_BRAF)

FUNC_START(sh2_int_BSR) /* BSR disp  1011dddddddddddd */
	__LOAD_IMM_S12x2 r4
	__SET_DELAY_SLOT_
	addi sh_pr, sh_pc, 4
	add sh_pc, sh_pr, r4
	__GET_PC_ADDR
	addic. rCycles, rCycles, 2
	__DO_DELAY_SLOT_
FUNC_END(sh2_int_BSR)

FUNC_START(sh2_int_BSRF) /* BSRF Rm  0000mmmm00000011 */
	__LOAD_REG_RN r4
	__SET_DELAY_SLOT_
	addi r3, sh_pc, 2
	addi sh_pr, sh_pc, 4
	add sh_pc, sh_pr, r4
	__GET_PC_ADDR
	addic. rCycles, rCycles, 2
	__DO_DELAY_SLOT_
FUNC_END(sh2_int_BSRF)


FUNC_START(sh2_int_BT) /* BT disp  10001001dddddddd */
	andi. r3, sh_sr, 1
	bc 0b01100, 2, bt_no_branch //T == 1 (we skip branch if [EQ] == 1)
	__LOAD_IMM_S8 r3
	rlwinm r3, r3, 1, 0, 30
	addi r3, r3, 4
	add sh_pc, sh_pc, r3
	__GET_PC_ADDR
	addic. rCycles, rCycles, 3
	b cycle_check
bt_no_branch:
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_BT)


FUNC_START(sh2_int_BTS) /* BTS disp  10001101dddddddd */
	andi. r3, sh_sr, 1
	bc 0b01100, 2, bts_no_branch //T == 1 (we skip branch if [EQ] == 1)
	__LOAD_IMM_S8 r3
	__SET_DELAY_SLOT_
	rlwinm r3, r3, 1, 0, 30
	addi r3, r3, 4
	add sh_pc, sh_pc, r3
	__GET_PC_ADDR
	addic. rCycles, rCycles, 2
	__DO_DELAY_SLOT_
bts_no_branch:
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_BTS)


FUNC_START(sh2_int_JMP) /* JMP @Rm  0100mmmm00101011 */
	__LOAD_REG_RN sh_pc
	__SET_DELAY_SLOT_
	__GET_PC_ADDR
	addic. rCycles, rCycles, 2
	__DO_DELAY_SLOT_
FUNC_END(sh2_int_JMP)

FUNC_START(sh2_int_JSR) /* JSR @Rm  0100mmmm00001011 */
	addi sh_pr, sh_pc, 4
	__LOAD_REG_RN sh_pc
	__SET_DELAY_SLOT_
	__GET_PC_ADDR
	addic. rCycles, rCycles, 2
	__DO_DELAY_SLOT_
FUNC_END(sh2_int_JSR)

FUNC_START(sh2_int_RTE) /* RTE  0000000000101011 */
	__SET_DELAY_SLOT_
	__LOAD_REG_R15 r3
	bl sh2_Read32
	mr sh_pc, r3
	__LOAD_REG_R15 r3
	addi r3, r3, 4
	addi r4, r3, 4
	__STORE_REG_R15 r4
	bl sh2_Read32
	andi. sh_sr, r3, 0x03F3
	__GET_PC_ADDR
	addic. rCycles, rCycles, 4
	__DO_DELAY_SLOT_
FUNC_END(sh2_int_RTE)

FUNC_START(sh2_int_RTS) /* RTS  0000000000001011 */
	__SET_DELAY_SLOT_
	mr sh_pc, sh_pr
	__GET_PC_ADDR
	addic. rCycles, rCycles, 2
	__DO_DELAY_SLOT_
FUNC_END(sh2_int_RTS)


/*Other*/
FUNC_START(sh2_int_NOP) /* NOP  0000000000001001 */
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_NOP)


FUNC_START(sh2_int_SLEEP) /* SLEEP  0000000000011011 */
	//?? SHOULD I ADD SLEEP STATE?
	addic. rCycles, rCycles, 3
	b cycle_check
FUNC_END(sh2_int_SLEEP)

FUNC_START(sh2_int_SWAPB) /* SWAP.B Rm,Rn  0110nnnnmmmm1000 */
	__LOAD_REG_RM r4
	mr r3, r4
	rlwimi r3, r4, 8, 16, 24
	rlwimi r3, r4, 32-8, 24, 31
	__STORE_REG_RN_ALL r3, r4
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_SWAPB)

FUNC_START(sh2_int_SWAPW) /* SWAP.W Rm,Rn  0110nnnnmmmm1001 */
	__LOAD_REG_RM r4
	rlwinm r3, r4, 16, 0, 31
	__STORE_REG_RN_ALL r3, r4
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_SWAPW)

FUNC_START(sh2_int_TAS) /* TAS.B @Rn  0100nnnn00011011 */
	__LOAD_REG_RN r3
	bl sh2_Read8
	cntlzw r4, r3
	rlwimi sh_sr, r4, 32-5, 31, 31
	ori r4, r3, 0x80
	__LOAD_REG_RN r3
	bl sh2_Write8
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 4
	b cycle_check
FUNC_END(sh2_int_TAS)

FUNC_START(sh2_int_TRAPA) /* TRAPA #imm  11000011iiiiiiii */
	__LOAD_REG_R15 r3
	addi r3, r3, -4
	mr r4, sh_sr
	bl sh2_Write32
	__LOAD_REG_R15 r3
	addi r3, r3, -8
	__STORE_REG_R15 r3
	addi r4, sh_pc, 2
	bl sh2_Write32
	rlwinm r3, sh_instr, 2, 22, 29
	add r3, r3, sh_vbr
	bl sh2_Read32
	mr sh_pc, r3
	addic. rCycles, rCycles, 8
	b cycle_check
FUNC_END(sh2_int_TRAPA)

FUNC_START(sh2_int_TST) /* TST Rm,Rn  0010nnnnmmmm1000 */
	__LOAD_REG_RN_RM r3, r4
	and.  r3, r3, r4
	mfcr  r3
	rlwimi sh_sr, r3, 3, 31, 31
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_TST)

FUNC_START(sh2_int_TSTI) /* TEST #imm,R0  11001000iiiiiiii */
	__LOAD_REG_R0 r3
	__LOAD_IMM_U8 r4
	and.  r3, r3, r4
	mfcr  r3
	rlwimi sh_sr, r3, 3, 31, 31
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_TSTI)

FUNC_START(sh2_int_TSTM) /* TST.B #imm,@(R0,GBR)  11001100iiiiiiii */
	__LOAD_REG_R0 r3
	add r3, r3, sh_gbr
	bl sh2_Read8
	__LOAD_IMM_U8 r4
	and.  r3, r3, r4
	mfcr  r3
	rlwimi sh_sr, r3, 3, 31, 31
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 3
	b cycle_check
FUNC_END(sh2_int_TSTM)

FUNC_START(sh2_int_XTRCT) /* XTRCT Rm,Rn  0010nnnnmmmm1101 */
	__LOAD_REG_RN_RM r3, r4
	rlwinm r3, r3, 16, 16, 31
	rlwimi r3, r4, 16, 0, 15
	__STORE_REG_RN r3
	addi sh_pc, sh_pc, 2
	addic. rCycles, rCycles, 1
	b cycle_check
FUNC_END(sh2_int_XTRCT)

FUNC_START(sh2_int_ILLEGAL) /* Illegal instruciton handler */
	__LOAD_REG_R15 r3
	addi r3, r3, -4
	mr r4, sh_sr
	bl sh2_Write32
	__LOAD_REG_R15 r3
	addi r3, r3, -8
	__STORE_REG_R15 r3
	addi r4, sh_pc, 2
	bl sh2_Write32
	addi r3, sh_vbr, 4 //TODO: Should be 6 when illegal delay slot (branch)
	bl sh2_Read32
	mr sh_pc, r3
	addic. rCycles, rCycles, 8
	b cycle_check
FUNC_END(sh2_int_ILLEGAL)


FUNC_START(sh2_IntExec)
	stwu r1, -92(r1)
	mflr r0
	stw r0, (92+4)(r1)
	stmw r14, (92-((31-14)*4))(r1)	//store host regs
	mr r31, r3						//get sh2 context
	lwz rCycles, (25*4)(r31)
	lwz sh_macl, (22*4)(r31)
	lwz sh_mach, (21*4)(r31)
	lwz sh_vbr, (20*4)(r31)
	lwz sh_gbr, (19*4)(r31)
	lwz sh_pr, (18*4)(r31)
	//lwz sh_pc, (17*4)(r31)
	//lwz sh_sr, (16*4)(r31)
	subf rCycles, r4, rCycles 		//sub cycles we need to run
	bl sh2_HandleInterrupt
	lwz sh_pc, (17*4)(r31)
	lwz sh_sr, (16*4)(r31)
	__GET_PC_ADDR 				//get pc address pointer

load_inst:
	//Load the insruction
	lhzu sh_instr, 2(sh_pca)
.decode_insr:
	//Load the opcode map
	//lis r3, .opcode_map@ha		//TODO: add it to nonvolatile?
	//ori r3, r3, .opcode_map@l
	//rlwinm r4, sh_instr, 21, 27, 30
	//lhax r4, r4, r3
	//Load the opcode tbl
	lis r3, .opcode_tbl@ha		//TODO: add it to nonvolatile?
	ori r3, r3, .opcode_tbl@l
	//Rotate the discriminating bits
	rlwinm r6, sh_instr, 32-4, 20, 23
	rlwimi r6, sh_instr, 2, 24, 29
	extsh. r5, sh_instr
	bc  0b00100, 0,	no_msb // do not negate if false
	rlwimi r6, sh_instr, 32-6, 26, 29
no_msb:
	//load and branch to the handler
	lwzx r3, r6, r3
	mtctr	r3
	bctr

cycle_check:
	bc 0b01100, 0, load_inst	//move to exit if cycles >= 0

exit: //Store all the regs
	stw rCycles, (25*4)(r31)
	stw sh_macl, (22*4)(r31)
	stw sh_mach, (21*4)(r31)
	stw sh_vbr, (20*4)(r31)
	stw sh_gbr, (19*4)(r31)
	stw sh_pr, (18*4)(r31)
	stw sh_pc, (17*4)(r31)
	stw sh_sr, (16*4)(r31)
	lwz r0, (92+4)(r1)
	mtlr r0
	lmw r14, (92 - ((31-14)*4))(r1)
	addi r1, r1, 92
	blr
FUNC_END(sh2_IntExec)

//	.section .sdata
	.balign 4
.opcode_tbl:
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_STCSR, sh2_int_BSRF
	.long sh2_int_MOVBS0, sh2_int_MOVWS0, sh2_int_MOVLS0, sh2_int_MULL
	.long sh2_int_CLRT, sh2_int_NOP, sh2_int_STSMACH, sh2_int_RTS
	.long sh2_int_MOVBL0, sh2_int_MOVWL0, sh2_int_MOVLL0, sh2_int_MACL
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_STCGBR, sh2_int_ILLEGAL
	.long sh2_int_MOVBS0, sh2_int_MOVWS0, sh2_int_MOVLS0, sh2_int_MULL
	.long sh2_int_SETT, sh2_int_DIV0U, sh2_int_STSMACL, sh2_int_SLEEP
	.long sh2_int_MOVBL0, sh2_int_MOVWL0, sh2_int_MOVLL0, sh2_int_MACL
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_STCVBR, sh2_int_BRAF
	.long sh2_int_MOVBS0, sh2_int_MOVWS0, sh2_int_MOVLS0, sh2_int_MULL
	.long sh2_int_CLRMAC, sh2_int_MOVT, sh2_int_STSPR, sh2_int_RTE
	.long sh2_int_MOVBL0, sh2_int_MOVWL0, sh2_int_MOVLL0, sh2_int_MACL
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_MOVBS0, sh2_int_MOVWS0, sh2_int_MOVLS0, sh2_int_MULL
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_MOVBL0, sh2_int_MOVWL0, sh2_int_MOVLL0, sh2_int_MACL
	.long sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4
	.long sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4
	.long sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4
	.long sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4
	.long sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4
	.long sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4
	.long sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4
	.long sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4
	.long sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4
	.long sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4
	.long sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4
	.long sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4
	.long sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4
	.long sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4
	.long sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4
	.long sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4, sh2_int_MOVLS4
	.long sh2_int_MOVBS, sh2_int_MOVWS, sh2_int_MOVLS, sh2_int_ILLEGAL
	.long sh2_int_MOVBM, sh2_int_MOVWM, sh2_int_MOVLM, sh2_int_DIV0S
	.long sh2_int_TST, sh2_int_AND, sh2_int_XOR, sh2_int_OR
	.long sh2_int_CMPSTR, sh2_int_XTRCT, sh2_int_MULU, sh2_int_MULS
	.long sh2_int_MOVBS, sh2_int_MOVWS, sh2_int_MOVLS, sh2_int_ILLEGAL
	.long sh2_int_MOVBM, sh2_int_MOVWM, sh2_int_MOVLM, sh2_int_DIV0S
	.long sh2_int_TST, sh2_int_AND, sh2_int_XOR, sh2_int_OR
	.long sh2_int_CMPSTR, sh2_int_XTRCT, sh2_int_MULU, sh2_int_MULS
	.long sh2_int_MOVBS, sh2_int_MOVWS, sh2_int_MOVLS, sh2_int_ILLEGAL
	.long sh2_int_MOVBM, sh2_int_MOVWM, sh2_int_MOVLM, sh2_int_DIV0S
	.long sh2_int_TST, sh2_int_AND, sh2_int_XOR, sh2_int_OR
	.long sh2_int_CMPSTR, sh2_int_XTRCT, sh2_int_MULU, sh2_int_MULS
	.long sh2_int_MOVBS, sh2_int_MOVWS, sh2_int_MOVLS, sh2_int_ILLEGAL
	.long sh2_int_MOVBM, sh2_int_MOVWM, sh2_int_MOVLM, sh2_int_DIV0S
	.long sh2_int_TST, sh2_int_AND, sh2_int_XOR, sh2_int_OR
	.long sh2_int_CMPSTR, sh2_int_XTRCT, sh2_int_MULU, sh2_int_MULS
	.long sh2_int_CMPEQ, sh2_int_ILLEGAL, sh2_int_CMPHS, sh2_int_CMPGE
	.long sh2_int_DIV1, sh2_int_DMULU, sh2_int_CMPHI, sh2_int_CMPGT
	.long sh2_int_SUB, sh2_int_ILLEGAL, sh2_int_SUBC, sh2_int_SUBV
	.long sh2_int_ADD, sh2_int_DMULS, sh2_int_ADDC, sh2_int_ADDV
	.long sh2_int_CMPEQ, sh2_int_ILLEGAL, sh2_int_CMPHS, sh2_int_CMPGE
	.long sh2_int_DIV1, sh2_int_DMULU, sh2_int_CMPHI, sh2_int_CMPGT
	.long sh2_int_SUB, sh2_int_ILLEGAL, sh2_int_SUBC, sh2_int_SUBV
	.long sh2_int_ADD, sh2_int_DMULS, sh2_int_ADDC, sh2_int_ADDV
	.long sh2_int_CMPEQ, sh2_int_ILLEGAL, sh2_int_CMPHS, sh2_int_CMPGE
	.long sh2_int_DIV1, sh2_int_DMULU, sh2_int_CMPHI, sh2_int_CMPGT
	.long sh2_int_SUB, sh2_int_ILLEGAL, sh2_int_SUBC, sh2_int_SUBV
	.long sh2_int_ADD, sh2_int_DMULS, sh2_int_ADDC, sh2_int_ADDV
	.long sh2_int_CMPEQ, sh2_int_ILLEGAL, sh2_int_CMPHS, sh2_int_CMPGE
	.long sh2_int_DIV1, sh2_int_DMULU, sh2_int_CMPHI, sh2_int_CMPGT
	.long sh2_int_SUB, sh2_int_ILLEGAL, sh2_int_SUBC, sh2_int_SUBV
	.long sh2_int_ADD, sh2_int_DMULS, sh2_int_ADDC, sh2_int_ADDV
	.long sh2_int_SHLL, sh2_int_SHLR, sh2_int_STSMMACH, sh2_int_STCMSR
	.long sh2_int_ROTL, sh2_int_ROTR, sh2_int_LDSMMACH, sh2_int_LDCMSR
	.long sh2_int_SHLL2, sh2_int_SHLR2, sh2_int_LDSMACH, sh2_int_JSR
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_LDCSR, sh2_int_MACW
	.long sh2_int_DT, sh2_int_CMPPZ, sh2_int_STSMMACL, sh2_int_STCMGBR
	.long sh2_int_ILLEGAL, sh2_int_CMPPL, sh2_int_LDSMMACL, sh2_int_LDCMGBR
	.long sh2_int_SHLL8, sh2_int_SHLR8, sh2_int_LDSMACL, sh2_int_TAS
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_LDCGBR, sh2_int_MACW
	.long sh2_int_SHAL, sh2_int_SHAR, sh2_int_STSMPR, sh2_int_STCMVBR
	.long sh2_int_ROTCL, sh2_int_ROTCR, sh2_int_LDSMPR, sh2_int_LDCMVBR
	.long sh2_int_SHLL16, sh2_int_SHLR16, sh2_int_LDSPR, sh2_int_JMP
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_LDCVBR, sh2_int_MACW
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_MACW
	.long sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4
	.long sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4
	.long sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4
	.long sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4
	.long sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4
	.long sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4
	.long sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4
	.long sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4
	.long sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4
	.long sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4
	.long sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4
	.long sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4
	.long sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4
	.long sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4
	.long sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4
	.long sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4, sh2_int_MOVLL4
	.long sh2_int_MOVBL, sh2_int_MOVWL, sh2_int_MOVLL, sh2_int_MOV
	.long sh2_int_MOVBP, sh2_int_MOVWP, sh2_int_MOVLP, sh2_int_NOT
	.long sh2_int_SWAPB, sh2_int_SWAPW, sh2_int_NEGC, sh2_int_NEG
	.long sh2_int_EXTUB, sh2_int_EXTUW, sh2_int_EXTSB, sh2_int_EXTSW
	.long sh2_int_MOVBL, sh2_int_MOVWL, sh2_int_MOVLL, sh2_int_MOV
	.long sh2_int_MOVBP, sh2_int_MOVWP, sh2_int_MOVLP, sh2_int_NOT
	.long sh2_int_SWAPB, sh2_int_SWAPW, sh2_int_NEGC, sh2_int_NEG
	.long sh2_int_EXTUB, sh2_int_EXTUW, sh2_int_EXTSB, sh2_int_EXTSW
	.long sh2_int_MOVBL, sh2_int_MOVWL, sh2_int_MOVLL, sh2_int_MOV
	.long sh2_int_MOVBP, sh2_int_MOVWP, sh2_int_MOVLP, sh2_int_NOT
	.long sh2_int_SWAPB, sh2_int_SWAPW, sh2_int_NEGC, sh2_int_NEG
	.long sh2_int_EXTUB, sh2_int_EXTUW, sh2_int_EXTSB, sh2_int_EXTSW
	.long sh2_int_MOVBL, sh2_int_MOVWL, sh2_int_MOVLL, sh2_int_MOV
	.long sh2_int_MOVBP, sh2_int_MOVWP, sh2_int_MOVLP, sh2_int_NOT
	.long sh2_int_SWAPB, sh2_int_SWAPW, sh2_int_NEGC, sh2_int_NEG
	.long sh2_int_EXTUB, sh2_int_EXTUW, sh2_int_EXTSB, sh2_int_EXTSW
	.long sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI
	.long sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI
	.long sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI
	.long sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI
	.long sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI
	.long sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI
	.long sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI
	.long sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI
	.long sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI
	.long sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI
	.long sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI
	.long sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI
	.long sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI
	.long sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI
	.long sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI
	.long sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI, sh2_int_ADDI
	.long sh2_int_MOVBS4, sh2_int_MOVWS4, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_MOVBL4, sh2_int_MOVWL4, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_CMPIM, sh2_int_BT, sh2_int_ILLEGAL, sh2_int_BF
	.long sh2_int_ILLEGAL, sh2_int_BTS, sh2_int_ILLEGAL, sh2_int_BFS
	.long sh2_int_MOVBS4, sh2_int_MOVWS4, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_MOVBL4, sh2_int_MOVWL4, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_CMPIM, sh2_int_BT, sh2_int_ILLEGAL, sh2_int_BF
	.long sh2_int_ILLEGAL, sh2_int_BTS, sh2_int_ILLEGAL, sh2_int_BFS
	.long sh2_int_MOVBS4, sh2_int_MOVWS4, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_MOVBL4, sh2_int_MOVWL4, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_CMPIM, sh2_int_BT, sh2_int_ILLEGAL, sh2_int_BF
	.long sh2_int_ILLEGAL, sh2_int_BTS, sh2_int_ILLEGAL, sh2_int_BFS
	.long sh2_int_MOVBS4, sh2_int_MOVWS4, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_MOVBL4, sh2_int_MOVWL4, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_CMPIM, sh2_int_BT, sh2_int_ILLEGAL, sh2_int_BF
	.long sh2_int_ILLEGAL, sh2_int_BTS, sh2_int_ILLEGAL, sh2_int_BFS
	.long sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI
	.long sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI
	.long sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI
	.long sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI
	.long sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI
	.long sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI
	.long sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI
	.long sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI
	.long sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI
	.long sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI
	.long sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI
	.long sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI
	.long sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI
	.long sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI
	.long sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI
	.long sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI, sh2_int_MOVWI
	.long sh2_int_BRA, sh2_int_BRA, sh2_int_BRA, sh2_int_BRA
	.long sh2_int_BRA, sh2_int_BRA, sh2_int_BRA, sh2_int_BRA
	.long sh2_int_BRA, sh2_int_BRA, sh2_int_BRA, sh2_int_BRA
	.long sh2_int_BRA, sh2_int_BRA, sh2_int_BRA, sh2_int_BRA
	.long sh2_int_BRA, sh2_int_BRA, sh2_int_BRA, sh2_int_BRA
	.long sh2_int_BRA, sh2_int_BRA, sh2_int_BRA, sh2_int_BRA
	.long sh2_int_BRA, sh2_int_BRA, sh2_int_BRA, sh2_int_BRA
	.long sh2_int_BRA, sh2_int_BRA, sh2_int_BRA, sh2_int_BRA
	.long sh2_int_BRA, sh2_int_BRA, sh2_int_BRA, sh2_int_BRA
	.long sh2_int_BRA, sh2_int_BRA, sh2_int_BRA, sh2_int_BRA
	.long sh2_int_BRA, sh2_int_BRA, sh2_int_BRA, sh2_int_BRA
	.long sh2_int_BRA, sh2_int_BRA, sh2_int_BRA, sh2_int_BRA
	.long sh2_int_BRA, sh2_int_BRA, sh2_int_BRA, sh2_int_BRA
	.long sh2_int_BRA, sh2_int_BRA, sh2_int_BRA, sh2_int_BRA
	.long sh2_int_BRA, sh2_int_BRA, sh2_int_BRA, sh2_int_BRA
	.long sh2_int_BRA, sh2_int_BRA, sh2_int_BRA, sh2_int_BRA
	.long sh2_int_BSR, sh2_int_BSR, sh2_int_BSR, sh2_int_BSR
	.long sh2_int_BSR, sh2_int_BSR, sh2_int_BSR, sh2_int_BSR
	.long sh2_int_BSR, sh2_int_BSR, sh2_int_BSR, sh2_int_BSR
	.long sh2_int_BSR, sh2_int_BSR, sh2_int_BSR, sh2_int_BSR
	.long sh2_int_BSR, sh2_int_BSR, sh2_int_BSR, sh2_int_BSR
	.long sh2_int_BSR, sh2_int_BSR, sh2_int_BSR, sh2_int_BSR
	.long sh2_int_BSR, sh2_int_BSR, sh2_int_BSR, sh2_int_BSR
	.long sh2_int_BSR, sh2_int_BSR, sh2_int_BSR, sh2_int_BSR
	.long sh2_int_BSR, sh2_int_BSR, sh2_int_BSR, sh2_int_BSR
	.long sh2_int_BSR, sh2_int_BSR, sh2_int_BSR, sh2_int_BSR
	.long sh2_int_BSR, sh2_int_BSR, sh2_int_BSR, sh2_int_BSR
	.long sh2_int_BSR, sh2_int_BSR, sh2_int_BSR, sh2_int_BSR
	.long sh2_int_BSR, sh2_int_BSR, sh2_int_BSR, sh2_int_BSR
	.long sh2_int_BSR, sh2_int_BSR, sh2_int_BSR, sh2_int_BSR
	.long sh2_int_BSR, sh2_int_BSR, sh2_int_BSR, sh2_int_BSR
	.long sh2_int_BSR, sh2_int_BSR, sh2_int_BSR, sh2_int_BSR
	.long sh2_int_MOVBSG, sh2_int_MOVWSG, sh2_int_MOVLSG, sh2_int_TRAPA
	.long sh2_int_MOVBLG, sh2_int_MOVWLG, sh2_int_MOVLLG, sh2_int_MOVA
	.long sh2_int_TSTI, sh2_int_ANDI, sh2_int_XORI, sh2_int_ORI
	.long sh2_int_TSTM, sh2_int_ANDM, sh2_int_XORM, sh2_int_ORM
	.long sh2_int_MOVBSG, sh2_int_MOVWSG, sh2_int_MOVLSG, sh2_int_TRAPA
	.long sh2_int_MOVBLG, sh2_int_MOVWLG, sh2_int_MOVLLG, sh2_int_MOVA
	.long sh2_int_TSTI, sh2_int_ANDI, sh2_int_XORI, sh2_int_ORI
	.long sh2_int_TSTM, sh2_int_ANDM, sh2_int_XORM, sh2_int_ORM
	.long sh2_int_MOVBSG, sh2_int_MOVWSG, sh2_int_MOVLSG, sh2_int_TRAPA
	.long sh2_int_MOVBLG, sh2_int_MOVWLG, sh2_int_MOVLLG, sh2_int_MOVA
	.long sh2_int_TSTI, sh2_int_ANDI, sh2_int_XORI, sh2_int_ORI
	.long sh2_int_TSTM, sh2_int_ANDM, sh2_int_XORM, sh2_int_ORM
	.long sh2_int_MOVBSG, sh2_int_MOVWSG, sh2_int_MOVLSG, sh2_int_TRAPA
	.long sh2_int_MOVBLG, sh2_int_MOVWLG, sh2_int_MOVLLG, sh2_int_MOVA
	.long sh2_int_TSTI, sh2_int_ANDI, sh2_int_XORI, sh2_int_ORI
	.long sh2_int_TSTM, sh2_int_ANDM, sh2_int_XORM, sh2_int_ORM
	.long sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI
	.long sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI
	.long sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI
	.long sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI
	.long sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI
	.long sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI
	.long sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI
	.long sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI
	.long sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI
	.long sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI
	.long sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI
	.long sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI
	.long sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI
	.long sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI
	.long sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI
	.long sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI, sh2_int_MOVLI
	.long sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI
	.long sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI
	.long sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI
	.long sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI
	.long sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI
	.long sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI
	.long sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI
	.long sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI
	.long sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI
	.long sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI
	.long sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI
	.long sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI
	.long sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI
	.long sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI
	.long sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI
	.long sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI, sh2_int_MOVI
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL
	.long sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL, sh2_int_ILLEGAL

